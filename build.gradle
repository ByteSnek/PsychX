plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.45'
    id 'net.neoforged.gradle.mixin' version '7.0.45'
}

version = minecraft_version + '-' + mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft.accessTransformers.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        systemProperty 'mixin.env.remapRefMap', 'true'
        systemProperty 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', mod_id
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

sourceSets.main {
    ext.refMap = mixinRefMap
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://s01.oss.sonatype.org/content/repositories/releases" }
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    implementation "bytesnek.snakerlib:SnakerLib:${minecraft_version}-${snakerlib_version}" setChanging(true)
}

mixin {
    configs = [mixinConfig]
}

tasks.withType(ProcessResources).configureEach {
    def replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
            pack_format_number     : pack_format_number,
            issue_tracker_url      : issue_tracker_url,
            logo_file_name         : logo_file_name,
            snakerlib_version_range: snakerlib_version_range,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    filesMatching("*.mixin.json") {
        expand "refmap_target": "${mod_id}."
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifacts = [jar, sourcesJar, javadocJar]

            pom {
                name = project.name
                description = 'A Minecraft Mod'
                url = 'https://github.com/ByteSnek/Tourniqueted'

                scm {
                    url = 'https://github.com/ByteSnek/Tourniqueted'
                    connection = 'scm:git:git://github.com/ByteSnek/Tourniqueted.git'
                    developerConnection = 'scm:git:git@github.com:ByteSnek/Tourniqueted.git'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/ByteSnek/Tourniqueted/LICENSE'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url "https://s01.oss.sonatype.org/content/repositories/releases"

            credentials {
                username 'snaker'
                password System.getenv('MAVEN_PASS')
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}