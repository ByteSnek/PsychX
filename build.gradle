plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    copyIdeResources = true
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file("run/${it.name}")

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

sourceSets.main {
    ext.refMap = mixinRefmap
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://s01.oss.sonatype.org/content/repositories/releases" }
    maven { url "https://maven.blamejared.com" }
    maven { url "https://cursemaven.com" }
    maven { url "https://jitpack.io" }
}

dependencies {
    minecraft "net.neoforged:forge:${minecraft_version}-${neo_version}"

    implementation fg.deobf("xyz.snaker.snakerlib:SnakerLib:${snakerlib_version}")
    implementation fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")
    implementation fg.deobf("codechicken:CodeChickenLib:1.20.1-4.4.0.1:universal")

    runtimeOnly fg.deobf("curse.maven:the-one-probe-245211:4629624")
    runtimeOnly fg.deobf("curse.maven:oauth-513857:4575495")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, mixinRefmap
    config mixinConfig
}

tasks.withType(ProcessResources).configureEach {
    def replaceProperties = [
            minecraft_version_range: minecraft_version_range,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
            snakerlib_version_range: snakerlib_version_range,
            patchouli_version_range: patchouli_version_range
    ]
    inputs.properties replaceProperties
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'        : mod_name,
                'Specification-Vendor'       : mod_authors,
                'Specification-Version'      : mod_version,
                'Implementation-Title'       : mod_name,
                'Implementation-Version'     : mod_version,
                'Implementation-Vendor'      : mod_authors,
                'Implementation-Timestamp'   : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'TweakClass'                 : mixinTweaker,
                'MixinConfigs'               : mixinConfig,
                'FMLCorePluginContainsFMLMod': 'true'
        ])
    }
    finalizedBy 'reobfJar'
}

tasks.register('docJar', Jar) {
    build.dependsOn it
    from javadoc
    archiveClassifier = 'javadoc'
}

tasks.register('srcJar', Jar) {
    build.dependsOn it
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
            artifact srcJar
            artifact docJar
            pom {
                name = project.name
                description = 'Modding Library'
                url = 'https://github.com/SnakerBone/Tourniqueted'
                scm {
                    url = 'https://github.com/SnakerBone/Tourniqueted'
                    connection = 'scm:git:git://github.com/SnakerBone/Tourniqueted.git'
                    developerConnection = 'scm:git:git@github.com:SnakerBone/Tourniqueted.git'
                }
                licenses {
                    license {
                        name = 'GPLv3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/content/repositories/releases"
            credentials {
                username 'snaker'
                password System.getenv('MAVEN_PASS')
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.deprecation(false)
    options.warnings(false)
}